@echo off
setlocal
rem ============================================================
rem gen_linemap.bat - after "lime build android", generate the
rem crash-time address->cpp-line map from the unstripped .so files.
rem
rem Requires python on PATH. pyelftools is installed automatically.
rem Usage:  tools\gen_linemap.bat [release^|debug]   (default: release)
rem
rem Outputs -> assets\linemap\arm64-v8a.bin + armeabi-v7a.bin
rem   * push to device:  adb push assets\linemap\arm64-v8a.bin
rem       /storage/emulated/0/Android/data/com.mohong.Seiunengine/files/linemap/
rem   * or rebuild the APK with -DCRASH_LINEMAP to embed them
rem ============================================================
cd /d "%~dp0.."

set BUILD=%1
if "%BUILD%"=="" set BUILD=release

where python >nul 2>nul
if errorlevel 1 (
    echo [gen_linemap] python not found on PATH.
    exit /b 1
)

python -c "import elftools" >nul 2>nul
if errorlevel 1 (
    echo [gen_linemap] installing pyelftools...
    python -m pip install pyelftools || exit /b 1
)

if not exist "export\%BUILD%\android\obj\libApplicationMain-64.so" (
    echo [gen_linemap] export\%BUILD%\android\obj\libApplicationMain-64.so not found.
    echo Run "lime build android" first.
    exit /b 1
)

echo [gen_linemap] arm64-v8a
python tools\gen_linemap.py "export\%BUILD%\android\obj\libApplicationMain-64.so" "assets\linemap\arm64-v8a.bin" || exit /b 1

if exist "export\%BUILD%\android\obj\libApplicationMain-v7.so" (
    echo [gen_linemap] armeabi-v7a
    python tools\gen_linemap.py "export\%BUILD%\android\obj\libApplicationMain-v7.so" "assets\linemap\armeabi-v7a.bin" || exit /b 1
)

echo.
echo [gen_linemap] Done. Enable embedding by building with:  lime build android -DCRASH_LINEMAP
echo [gen_linemap] Or push to device:
echo   adb push assets\linemap\arm64-v8a.bin /storage/emulated/0/Android/data/com.mohong.Seiunengine/files/linemap/
endlocal
